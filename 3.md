#### Truth values and conditional expressions

```
def select_first = λfirst.λsecond.first
def select_second = λfirst.λsecond.second
def true = select_first
def false = select_second
def cond = λe1.λe2.λc.((c e1) e2)
```

### NOT

| p | ⌐p  |
| - | :-: |
| T |  F  |
| F |  T  |

```
def not = λx.(((cond false) true) x)
```

body simplification

```
(((λe1.λe2.λc.((c e1) e2) false) true) x) =>
((λe2.λc.((c false) e2) true) x) =>
(λc.((c false) true) x) => 
((x false) true)
```

gives

```
def not = λx.((x false) true)
```

```
(not true) ==
(λx.((x false) true) true) =>
((true false) true) ==
((λfirst.λsecond.first false) true) =>
(λsecond.false true) =>
false
```

```
(not false) ==
(λx.((x false) true) false) =>
((false false) true) ==
((λfirst.λsecond.second false) true) =>
(λsecond.second true) =>
true
```

### AND

| p | q | p ∧ q |
| - | - | :---: |
| F | F |   F   |
| F | T |   F   |
| T | F |   F   |
| T | T |   T   |

```
def and = λx.λy.(((cond y) false) x)
```

body simplification

```
(((λe1.λe2.λc.((c e1) e2) y) false) x) =>
((λe2.λc.((c y) e2) false) x) =>
(λc.((c y) false) x) =>
((x y) false)
```

gives

```
def and = λx.λy.((x y) false)
```

### OR

| p | q | p ∨ q |
| - | - | :---: |
| F | F |   F   |
| F | T |   T   |
| T | F |   T   |
| T | T |   T   |